buildscript {
  repositories {
    google()
    mavenCentral()
    maven {
      url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.1"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["CompdfkitPdf_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["CompdfkitPdf_" + name]).toInteger()
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.compdfkitpdf.reactnative"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")

  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenCentral()
  google()
  maven { url 'https://central.sonatype.com/repository/maven-snapshots/' }
}


dependencies {
  compileOnly fileTree(include: ['*.jar','*.aar'], dir: 'libs')
  implementation "com.facebook.react:react-native:+"

  api "com.compdf:compdfkit-tools:2.4.6"
  api 'com.github.bumptech.glide:glide:4.15.1'
  annotationProcessor 'com.github.bumptech.glide:compiler:4.15.1'
  api 'androidx.documentfile:documentfile:1.0.1'
  api 'androidx.appcompat:appcompat:1.6.1'
  api 'com.google.android.material:material:1.8.0'
  api 'androidx.constraintlayout:constraintlayout:2.1.4'
}
configurations.all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }


